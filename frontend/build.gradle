plugins {
    id 'base'
    id 'com.github.node-gradle.node'
}


node {
    version = '14.16.0'
    download = true
}


npm_run_build {
    inputs.files fileTree("public")
    inputs.files fileTree("src")

    inputs.file 'package.json'
    inputs.file 'package-lock.json'

    outputs.dir 'build'
}

task start(type: NpmTask) {
    args = ['start']
}

//task test(type: NpmTask) {
//    args = ['test']
//}

task packageClient(type: Zip) {
    dependsOn npm_run_build
    baseName 'ynabsplitter-frontend'
    extension 'jar'

    destinationDir file("${projectDir}/build_packageClient")

    from('build') {
        into 'META-INF/resources'
    }
}

assemble.dependsOn packageClient

configurations {
    npmResources
}
configurations.default.extendsFrom(configurations.npmResources)

artifacts {
    npmResources(packageClient.archivePath) {
        builtBy packageClient
        type "jar"
    }
}

clean {
    delete packageClient.archivePath
}
